plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id "me.shedaniel.unified-publishing" version "0.1.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand(project.properties)
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

unifiedPublishing {
	project {
		displayName = "[${project.supported_versions_string}] ${project.mod_name} ${project.mod_version}"
		version = "${project.mod_version}+${project.minecraft_version}"
		changelog = "${project.changelog}"
		releaseType = "${project.release_type}"
		gameVersions = List.of(project.supported_versions.split(';'))
		gameLoaders = ["fabric"]

		mainPublication tasks.remapJar

		if (System.getenv("CURSEFORGE_TOKEN")) {
			curseforge {
				token = System.getenv("CURSEFORGE_TOKEN")
				id = rootProject.curseforge_id
			}
		}

		if (System.getenv("MODRINTH_TOKEN")) {
			modrinth {
				token = System.getenv("MODRINTH_TOKEN")
				id = rootProject.modrinth_id
			}
		}
	}
}
